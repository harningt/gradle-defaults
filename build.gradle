buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.7"
    }
}

plugins {
  id 'groovy'
  id 'java-gradle-plugin'
  id 'us.eharning.gradle.defaults' version '0.1.2'
}

apply plugin: "com.gradle.plugin-publish"

repositories {
    jcenter()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

group = 'us.eharning.gradle'
version = '0.1.2'

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.ajoberstar:grgit:1.9.1'
    compile 'org.ajoberstar:gradle-git:1.7.2'
    compile 'org.ajoberstar:gradle-git-publish:0.2.1'
    compile 'org.ajoberstar:gradle-stutter:0.1.0'
    compile 'org.ajoberstar:gradle-bintray:0.1.0'
    compile ('org.ajoberstar:semver-vcs-gradle-grgit:0.1.0') {
        exclude module: 'groovy-all'
    }
    //compile 'com.gradle.publish:plugin-publish-plugin:0.9.7'
    compile "com.diffplug.spotless:spotless-plugin-gradle:3.4.0"
    compile "org.standardout:gradle-versioneye-plugin:1.4.0"
    compile "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0"
}

versioneye {
    /* Workaround for Gradle 4 issue */
    exclude project.configurations.findAll { !it.canBeResolved }*.name as String[]
}

checkstyle {
    toolVersion = "7.8.2"
}

pluginBundle {
    website = 'https://github.com/harningt/gradle-defaults'
    vcsUrl = 'https://github.com/harningt/gradle-defaults'
    description = 'Plugin providing opinionated defaults for us.eharning.* Gradle projects'
    plugins {
        publishPlugin {
            id = 'us.eharning.gradle.defaults'
            displayName = 'us.eharning.* Defaults Plugin'
            tags = ['defaults']
        }
    }
    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
        version = project.version
    }
}

wrapper {
    gradleVersion = '4.0'
}
